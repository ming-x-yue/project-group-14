plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'jacoco'
}

group = 'ca.mcgill.ecse321'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.mockito:mockito-core:5.8.0'
	testImplementation 'org.mockito:mockito-junit-jupiter:5.8.0'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
	testImplementation 'io.cucumber:cucumber-java:7.14.0'
	testImplementation 'io.cucumber:cucumber-spring:7.14.0'
	testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.14.0'
	testImplementation 'org.junit.platform:junit-platform-suite:1.10.1'
	testImplementation 'org.junit.platform:junit-platform-console:1.10.1'
	testImplementation 'org.seleniumhq.selenium:selenium-java:4.16.1'
	testImplementation 'io.github.bonigarcia:webdrivermanager:5.6.2'
}

tasks.named('test') {
	useJUnitPlatform()
	testLogging {
		events "passed", "failed", "skipped"
	}
	finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
}

// Add a custom task to run only Cucumber tests
task cucumberTests() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            mainClass = "org.junit.platform.console.ConsoleLauncher"
            classpath = sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
            args = ["--include-engine", "cucumber", "--scan-classpath"]
            systemProperty 'cucumber.junit-platform.naming-strategy', 'long'
            systemProperty 'cucumber.glue', 'ca.mcgill.ecse321.sportCenterRegistration.steps'
            systemProperty 'cucumber.plugin', 'pretty'
        }
    }
}